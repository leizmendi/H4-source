Option Compare Database
Option Explicit

'************** Code Start ***************
'CONSEGUIR LA CLASE DE UNA APLICACIÓN
' This code was originally written by Dev Ashish.
' It is not to be altered or distributed,
' except as part of an application.
' You are free to use it in any application,
' provided the copyright notice is left unchanged.
'
' Code Courtesy of
' Dev Ashish
'
Private Declare Function apiGetClassName Lib "user32" Alias _
                "GetClassNameA" (ByVal Hwnd As Long, _
                ByVal lpClassName As String, _
                ByVal nMaxCount As Long) As Long
Private Declare Function apiGetDesktopWindow Lib "user32" Alias _
                "GetDesktopWindow" () As Long
Private Declare Function apiGetWindow Lib "user32" Alias _
                "GetWindow" (ByVal Hwnd As Long, _
                ByVal wCmd As Long) As Long
Private Declare Function apiGetWindowLong Lib "user32" Alias _
                "GetWindowLongA" (ByVal Hwnd As Long, ByVal _
                nIndex As Long) As Long
Private Declare Function apiGetWindowText Lib "user32" Alias _
                "GetWindowTextA" (ByVal Hwnd As Long, ByVal _
                lpString As String, ByVal aint As Long) As Long
Private Const mcGWCHILD = 5
Private Const mcGWHWNDNEXT = 2
Private Const mcGWLSTYLE = (-16)
Private Const mcWSVISIBLE = &H10000000
Private Const mconMAXLEN = 255

Function fEnumWindows()
Dim lngX As Long, lngLen As Long
Dim lngStyle As Long, strCaption As String
    
    lngX = apiGetDesktopWindow()
    'Return the first child to Desktop
    lngX = apiGetWindow(lngX, mcGWCHILD)
    
    Do While Not lngX = 0
        strCaption = fGetCaption(lngX)
        If Len(strCaption) > 0 Then
            lngStyle = apiGetWindowLong(lngX, mcGWLSTYLE)
            'enum visible windows only
            If lngStyle And mcWSVISIBLE Then
                Debug.Print "Class = " & fGetClassName(lngX),
                Debug.Print "Caption = " & fGetCaption(lngX)
            End If
        End If
        lngX = apiGetWindow(lngX, mcGWHWNDNEXT)
    Loop
End Function
Function DimeClassName(strTitulo As String) As String
Dim lngX As Long, lngLen As Long
Dim lngStyle As Long, strCaption As String
    
    lngX = apiGetDesktopWindow()
    'Return the first child to Desktop
    lngX = apiGetWindow(lngX, mcGWCHILD)
    
    Do While Not lngX = 0
        strCaption = fGetCaption(lngX)
        If InStr(strCaption, strTitulo) = 1 Then
            DimeClassName = fGetClassName(lngX)
            Exit Function
        End If
        lngX = apiGetWindow(lngX, mcGWHWNDNEXT)
    Loop
    DimeClassName = ""
End Function
Private Function fGetClassName(Hwnd As Long) As String
    Dim strBuffer As String
    Dim intCount As Integer
   
    strBuffer = String$(mconMAXLEN - 1, 0)
    intCount = apiGetClassName(Hwnd, strBuffer, mconMAXLEN)
    If intCount > 0 Then
        fGetClassName = Left$(strBuffer, intCount)
    End If
End Function

Private Function fGetCaption(Hwnd As Long) As String
    Dim strBuffer As String
    Dim intCount As Integer

    strBuffer = String$(mconMAXLEN - 1, 0)
    intCount = apiGetWindowText(Hwnd, strBuffer, mconMAXLEN)
    If intCount > 0 Then
        fGetCaption = Left$(strBuffer, intCount)
    End If
End Function
'************** Code End ***************