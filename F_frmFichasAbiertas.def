CodeBehindForm
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Compare Database
Option Explicit

Private Sub btnAdd_Click()
    Dim rs As Recordset, lngIdFichaCab As Long
    Set rs = CurrentDb.OpenRecordset("tbFichasCab", dbOpenDynaset)
    rs.AddNew
    lngIdFichaCab = Nz(DMax("IdFichaCab", "tbFichasCab"), 0) + 1
    rs("IdFichaCab") = lngIdFichaCab
    rs.Update
    AbrirFichaNum lngIdFichaCab
    Me.Visible = False
End Sub

Private Sub btnEdit_Click()
    If Me.lstActuales.ListIndex = -1 Then Exit Sub
    Call AbrirFichaNum(Me.lstActuales)
    Me.Visible = False
End Sub

Private Sub btnSalir_Click()
    DoCmd.Close acForm, Me.Name
End Sub

Private Sub Form_KeyDown(KeyCode As Integer, Shift As Integer)
    ControlTeclas Me, KeyCode, Shift, ""
End Sub

Private Sub Form_Load()
    Me.mrcTS = Nz(DameValorParam(Me.Name & "_mrcTS"), 2)
    Call mrcTS_AfterUpdate
End Sub

Private Sub lstActuales_DblClick(Cancel As Integer)
    Call btnEdit_Click
End Sub

Private Sub lstActuales_KeyDown(KeyCode As Integer, Shift As Integer)
    If KeyCode = vbKeyReturn Then
        Call btnEdit_Click
    End If
End Sub

Private Sub mrcTS_AfterUpdate()
    Dim strSQL As String
    If g_strPH = "" Then
        Call TipoAplicacion
    End If
    strSQL = "SELECT DISTINCT tbFichasCab.IdFichaCab AS [Nº Ficha], ParcelasDeUnaFicha([tbFichasCab]![idFichaCab]) AS [" & g_strPH & "], tbFichasCab.FechaLlegada AS Fecha, [NombreCliente] & ' ' & [ApellidosCliente] AS Cliente, [NombreCli] & ' ' & [ApellidosCli] AS Empresa, LlenaIzq(Format([TotalBase]+[TotalIVA]+[TotalRecEq],'Currency'),12) AS Importe"
    strSQL = strSQL & " FROM ((tbFichasCab LEFT JOIN tbClientes ON tbFichasCab.IdAgencia = tbClientes.IdCliente) LEFT JOIN tbFichasCab_Totales ON tbFichasCab.IdFichaCab = tbFichasCab_Totales.IdFichaCab) LEFT JOIN tbFichasDet ON tbFichasCab.IdFichaCab = tbFichasDet.IdFichaCab"
    Select Case Me.mrcTS
        Case 1, 2
            strSQL = strSQL & " WHERE tbFichasCab.EstadoFicha=2"
            If Me.mrcTS = 2 Then
                strSQL = strSQL & " AND ParcelasDeUnaFicha([tbFichasCab]![IdFichaCab])=''"
            End If
        Case 3
            strSQL = strSQL & " WHERE tbFichasCab.EstadoFicha=10"
    End Select
        
    strSQL = strSQL & " ORDER BY [tbFichasCab]![IdFichaCab]"
    Me.lstActuales.RowSource = strSQL
End Sub

Private Sub mrcTS_Click()
    PonValorParam Me.Name & "_mrcTS", Me.mrcTS, 4
End Sub
